# --
# scripts/DBUpdateTo12/InvalidSettingsCheck.pm
# Modified version of the work:
# Copyright (C) 2010-2025 OFORK, https://o-fork.de
# based on the original work of:
# Copyright (C) 2001-2018 OTRS AG, http://otrs.com/
# --
# $Id: InvalidSettingsCheck.pm,v 1.1.1.1 2018/07/16 14:49:06 ud Exp $
# --

package scripts::DBUpdateTo12::InvalidSettingsCheck;

use strict;
use warnings;

use IO::Interactive qw(is_interactive);

use parent qw(scripts::DBUpdateTo12::Base);

our @ObjectDependencies = (
    'Kernel::System::Cache',
    'Kernel::System::Console::Command::Admin::Config::FixInvalid',
    'Kernel::System::Main',
    'Kernel::System::SysConfig',
);

=head1 NAME

scripts::DBUpdateTo12::InvalidSettingsCheck - Checks for invalid configuration settings.

=cut

sub Run {
    my ( $Self, %Param ) = @_;

    my $Verbose = $Param{CommandlineOptions}->{Verbose} || 0;

    my $SysConfigObject = $Kernel::OM->Get('Kernel::System::SysConfig');

    my @InvalidSettings = $SysConfigObject->ConfigurationInvalidList(
        Undeployed => 1,    # Check undeployed settings as well.
        NoCache    => 1,
    );

    return 1 if !scalar @InvalidSettings;

    my $CommandObject = $Kernel::OM->Get('Kernel::System::Console::Command::Admin::Config::FixInvalid');

    # Run in no-ANSI mode for consistent output. Skip missing settings (they might get fixed later).
    my @CommandArgs = ( '--no-ansi', '--skip-missing' );

    # Capture the output if we are in non-interactive mode.
    if ( $Param{CommandlineOptions}->{NonInteractive} || !is_interactive() ) {

        # Try to fix invalid settings automatically.
        push @CommandArgs, '--non-interactive';

        my $CommandOutput;

        {
            local *STDOUT;
            open STDOUT, '>:utf8', \$CommandOutput;    ## no critic
            $CommandObject->Execute(@CommandArgs);
        }

        # Show command output in following cases:
        #   - verbose mode is active
        #   - warnings were raised for settings
        #   - settings were auto-corrected
        #
        #   Any errors printed to STDERR will show up regardless.
        if (
            $CommandOutput
            && (
                $Verbose
                || $CommandOutput =~ m{Warning:}
                || $CommandOutput =~ m{Auto-corrected setting:}
            )
            )
        {
            print $CommandOutput;
        }
    }

    # Otherwise, just execute the command and show complete output as-is.
    else {
        $CommandObject->Execute(@CommandArgs);
    }

    # Always return success, as warnings generated by this module are not considered fatal.
    return 1;
}

1;
